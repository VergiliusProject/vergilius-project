plugins {
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'io.freefair.lombok' version '9.0.0'
    id 'java'
}

group 'com.vergiliusproject'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java:8.0.33'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework:spring-core') {
        exclude module: 'commons-logging'
    }

    implementation 'de.neuland-bfi:pug4j:2.3.1'
    implementation 'de.neuland-bfi:spring-pug4j:3.3.1'
    implementation 'org.apache.commons:commons-lang3:3.19.0'
    implementation 'org.apache.commons:commons-text:1.14.0'

    implementation 'org.yaml:snakeyaml:2.4'

    implementation 'org.hibernate.orm:hibernate-jcache:6.6.18.Final'
    implementation 'org.ehcache:ehcache:3.10.8:jakarta'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'com.h2database:h2:2.4.240'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
}

bootRun {
    systemProperty "GIT_HASH", getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {  
    'git rev-parse --verify --short HEAD'.execute([], project.projectDir).text.trim()
}